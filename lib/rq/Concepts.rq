-- Create concept triples (s/b 42)

PREFIX oap: <http://experts.ucdavis.edu/oap/vocab#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
INSERT { 
	GRAPH <http://experts.ucdavis.edu/pubs/> {
		?conceptURI a skos:Concept .
		?conceptURI rdfs:label ?keyword .
	}
}
WHERE{
	SELECT
    	DISTINCT ?conceptURI ?keyword
    WHERE {
		GRAPH <http://experts.ucdavis.edu/oap/> {
			?publication oap:all-labels/oap:keywords/oap:keyword [ oap:field-value ?keyword ; oap:scheme 'for' ] .
			BIND(REPLACE(REPLACE(LCASE(STR(?keyword)), '[^\\w\\d]','-'), '-{2,}' ,'-') AS ?keywordIdText)
			BIND(URI(CONCAT("http://experts.ucdavis.edu/term/for:", ?keywordIdText)) AS ?conceptURI)
		}
  	}
}

-- Create triples connecting publication to concept (s/b 1,459)

PREFIX oap: <http://experts.ucdavis.edu/oap/vocab#>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
INSERT { 
	GRAPH <http://experts.ucdavis.edu/pubs/> {
		?publication vivo:hasSubjectArea ?conceptURI .
	}
}
WHERE {
  	GRAPH <http://experts.ucdavis.edu/oap/> {
		?publication oap:all-labels/oap:keywords/oap:keyword [ oap:field-value ?keyword ; oap:scheme 'for' ] .
	}
	# Grab the concepts just created
  	GRAPH <http://experts.ucdavis.edu/pubs/> {
		?conceptURI rdfs:label ?keyword
	}
} 
