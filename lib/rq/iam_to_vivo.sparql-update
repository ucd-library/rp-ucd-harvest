PREFIX experts: <http://experts.library.ucdavis.edu/individual/>
PREFIX experts_iam: <http://experts.ucdavis.edu/iam/>
PREFIX iam: <http://experts.ucdavis.edu/iam/schema#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ucd: <http://experts.ucdavis.edu/schema#>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

INSERT {
  GRAPH <http://iam.ucdavis.edu/> {
  ?user a ?emp_type;
        rdfs:label ?label;
        ucd:casId ?kerb;
        obo:ARG_2000028 [
          a vcard:Individual;
          ucd:identifier ?vid;
          vivo:rank ?order;
          vcard:hasName [a vcard:Name;
            vcard:givenName ?fname;
            vcard:familyName ?lname;
          ];
          vcard:hasTitle [a vcard:Title; vcard:title ?title];
          vcard:hasOrganizationalUnit [a vcard:Organization; vcard:title ?dept];
          vcard:hasEmail [a vcard:Email,vcard:Work; vcard:email ?email];
       ];
  }
}  WHERE {
  select ?user ?label ?vid ?title ?email ?dept ?fname ?lname ?order ?emp_type
  where {
    graph ?g {
      ?s iam:email ?email;
         iam:userID ?kerb;
         iam:dLastName ?iam_lname;
         iam:dFirstName ?iam_fname;
         iam:isFaculty ?faculty
      .
      OPTIONAL {
        {
        select ?s ?vid ?better_fname ?better_lname ?title ?dept ?order
        WHERE {
         	?s iam:directory ?dir .
            ?dir iam:listings ?list;
    	         iam:displayName [ iam:nameWwwFlag "Y";
                                   iam:preferredFname ?better_fname;
                                   iam:preferredLname ?better_lname ];
               .
            OPTIONAL {
              ?list iam:deptWwwFlag "Y";
                iam:listingOrder ?order;
                iam:title ?title;
                iam:deptName ?dept;
              .
              bind(concat("odr-",str(?order)) as ?vid)
          }
        }
      } UNION {
        select ?s ?vid ?better_fname ?better_lname ?title ?dept ?order where
        {
          ?s iam:ppsAssociations [iam:assocRank ?a_order;
                                  iam:titleOfficialName ?otitle;
                                  iam:deptOfficialName ?dept ];
                 .
          bind(replace(?otitle," -.*","") as ?title)
          bind(xsd:integer(?a_order)+10 as ?order)
          bind(concat("pps-",str(?a_order)) as ?vid)
        }
      }
    }
  }
  bind(coalesce(?better_fname,?iam_fname) as ?fname)
  bind(coalesce(?better_lname,?iam_lname) as ?lname)
  bind(concat(?fname," ",?lname) as ?label)
  bind(uri(concat(str(experts:),?kerb)) as ?user)
  bind(if(?faculty=true,vivo:FacultyMember,vivo:NonAcademic) as ?emp_type)
} order by ?user ?label
}
