-- Conference papers

PREFIX oap: <http://experts.ucdavis.edu/oap/vocab#>
PREFIX cite: <http://citationstyles.org/schema/>
PREFIX bibo: <http://purl.org/ontology/bibo/>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX list: <http://jena.apache.org/ARQ/list#>
PREFIX experts: <http://experts.ucdavis.edu/> 
PREFIX pubs: <http://experts.ucdavis.edu/pubs/> 
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#> 
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

INSERT { 
	GRAPH <http://experts.ucdavis.edu/pubs/> {
		?publication a vivo:ConferencePaper ;
				rdfs:label ?title ;
				bibo:issue ?issue ;
				bibo:volume ?volume ;
				bibo:number ?number ;
				bibo:abstract ?abstract ;
				bibo:doi ?doi ;
				bibo:isbn10 ?isbn10 ;
				bibo:isbn13 ?isbn13 ;
				bibo:uri ?pubExternalURL ;
				#bibo:presentedAt [ a owl:Event ; rdfs:label ?nameOfConference ] ;
				bibo:pageStart ?beginPage ;
				bibo:pageEnd ?endPage ;
				bibo:status ?vivoStatus ;
				vivo:hasPublicationVenue ?journalURI ;
				vivo:relatedBy _:authorship .
		_:authorship a vivo:Authorship ;
					 vivo:rank ?authorRank ;
					 vivo:relates ?personURI ;
					 vivo:relates ?publication ;
					 vivo:relates [ a vcard:Individual ;
									vivo:relatedBy _:authorship ;
									vcard:hasName [ a vcard:Name ;
													vcard:last_name ?authorLastName ;
													vcard:first_name ?authorFirstName ;
												  ] ;
								  ] .
		?journalURI a bibo:Journal ;
				vivo:publicationVenueFor ?publication ;
				rdfs:label ?journalTitle ;
				bibo:issn ?issn ;
				bibo:eissn ?eissn.
	}
}	
WHERE { 
	GRAPH <http://experts.ucdavis.edu/oap/> {
		?publication oap:records/oap:record ?exemplarRecord .
		#Authors
		?exemplarRecord oap:native/oap:field [ oap:name "authors" ; oap:people/oap:person [ list:index(?pos ?elem) ] ] .
		?elem oap:last-name ?authorLastName ;
			  oap:first-names ?authorFirstName .
		BIND(?pos+1 AS ?authorRank)
		#Link to UCD user for future wire-up to Person		
        OPTIONAL { 
        	?elem oap:links/oap:link ?userLink.
      		?userLink  oap:username ?username .
      		BIND(URI(CONCAT(STR("http://experts.ucdavis.edu/"),STRBEFORE(?username,"@"))) AS ?personURI)
      	}
		?exemplarRecord oap:native/oap:field [ oap:name "title" ; oap:text ?title ] .
		OPTIONAL {
			?exemplarRecord oap:native/oap:field [ oap:name "issue" ; oap:text ?issue ].
		}
		OPTIONAL {
			?exemplarRecord oap:native/oap:field [ oap:name "volume" ; oap:text ?volume ].
		} 
		OPTIONAL {
			?exemplarRecord oap:native/oap:field [ oap:name "number" ; oap:text ?number ].
		} 		
		OPTIONAL {
			?exemplarRecord oap:native/oap:field [ oap:name "abstract" ; oap:text ?abstract ].
		}       
		OPTIONAL {
			?exemplarRecord oap:native/oap:field [ oap:name "doi" ; oap:text ?doi ].
		}
		OPTIONAL {
			?exemplarRecord oap:native/oap:field [ oap:name "isbn-10" ; oap:text ?isbn10 ].
		}				
		OPTIONAL {
			?exemplarRecord oap:native/oap:field [ oap:name "isbn-13" ; oap:text ?isbn13 ].
		}
		# Only Crossref has name-of-conference, so it may not be worth it. Results in combinatorial expnasion b/c of blank nodes
		OPTIONAL {
			?exemplarRecord oap:native/oap:field [ oap:name "name-of-conference" ; oap:text ?nameOfConference ].
		}								
		OPTIONAL {
			?exemplarRecord oap:native/oap:field [ oap:name "pagination" ; oap:pagination [ oap:begin-page ?beginPage ; oap:end-page ?endPage ] ].
		}
		OPTIONAL {
			?exemplarRecord oap:native/oap:field [ oap:name "journal" ; oap:text ?journalTitle ].
			BIND(REPLACE(REPLACE(LCASE(STR(?journalTitle)), '[^\\w\\d]','-'), '-{2,}' ,'-') AS ?journalIdText)
		}
		OPTIONAL {
			?exemplarRecord oap:native/oap:field [ oap:name "eissn" ; oap:text ?eissn ].
		}				
		OPTIONAL {
			?exemplarRecord oap:native/oap:field [ oap:name "issn" ; oap:text ?issn ].
		}
		OPTIONAL {
			VALUES ?pubExternalURLField {'author-url' 'public-url' }
			?exemplarRecord oap:native/oap:field [ oap:name ?pubExternalURLField ; oap:text ?pubExternalURL ].
		}	
		OPTIONAL {
			VALUES (?status ?vivoStatus) { ( "Published" bibo:published ) ( "Published online" bibo:published ) ( "Accepted" bibo:accepted ) }		
			?exemplarRecord oap:native/oap:field [ oap:name "publication-status" ; oap:text ?status ]
		}			
		BIND(URI(CONCAT("http://experts.ucdavis.edu/pub/", COALESCE(CONCAT("eissn:", ?eissn), CONCAT("issn:", ?issn), CONCAT("journal:", ?journalIdText)))) AS ?journalURI)
		{
		SELECT 
			?publication (MIN(?record) AS ?exemplarRecord) 
		WHERE { 
			# Map the priority back to its source-name
			VALUES (?sourceNameA ?minPriority) {
				("verified-manual" 1)
				("epmc" 2)
				("pubmed" 3) 
				("scopus" 4)
				("wos" 5)
				("wos-lite" 6)
				("crossref" 7)
				("dimensions" 8)
				("arxiv" 9)
				("orcid" 10)
				("dblp" 11)
				("cinii-english" 12)
				("repec" 13)
				("figshare" 14)
				("cinii-japanese" 15)
				("manual" 16)
				("dspace" 17)
			}
			?publication oap:category "publication" ; 
						 oap:type "conference" ;
						 oap:records/oap:record ?record .
			?record oap:source-name  ?sourceNameA 
			{
			SELECT 
				?publication (MIN(?priority) AS ?minPriority)
			WHERE { 
				# Map the source-name to its priority
				VALUES (?sourceNameIQ ?priority) {
					("verified-manual" 1)
					("epmc" 2)
					("pubmed" 3) 
					("scopus" 4)
					("wos" 5)
					("wos-lite" 6)
					("crossref" 7)
					("dimensions" 8)
					("arxiv" 9)
					("orcid" 10)
					("dblp" 11)
					("cinii-english" 12)
					("repec" 13)
					("figshare" 14)
					("cinii-japanese" 15)
					("manual" 16)
					("dspace" 17)
				}
				?publication oap:category "publication" ;
							 oap:type "conference" ;
							 oap:records/oap:record/oap:source-name ?sourceNameIQ
			  }
			  GROUP BY 
				?publication
			}
		}
		GROUP BY
			?publication 
		}
	}
}

-- Their presentation locus (?)
